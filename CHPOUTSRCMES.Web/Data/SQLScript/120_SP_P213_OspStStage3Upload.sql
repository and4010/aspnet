-- =============================================
-- Author:		Polo Lin
-- Create date: 2020/09/16
-- Description:	SOA XXIF_CHP_P213_IN_BATCH_COM_ST 資料上傳
-- 未解決.. RXD  捲筒理論重
-- =============================================
CREATE PROCEDURE [dbo].[SP_P213_OspStStage3Upload]
	@ospHeaderId BIGINT,
	@code INT OUTPUT,
	@message VARCHAR(500) OUTPUT,
	@user VARCHAR(128)
AS
BEGIN

	DECLARE @table TABLE (
		PROCESS_CODE VARCHAR(20)
		, SERVER_CODE VARCHAR(20)
		, BATCH_ID VARCHAR(20)
		, BATCH_LINE_ID BIGINT
		, BATCH_NO VARCHAR(32) 
	) 

	DECLARE @success INT = 0
	DECLARE @count INT = 0
	SELECT @message = 'OSP_HEADER_ID:' + CONVERT(varchar, @ospHeaderId)

	DECLARE @txnDate DATETIME = GETDATE()
	DECLARE @processCode VARCHAR(20) = 'XXIFP213', @serverCode VARCHAR(20) = 'FTY'
	DECLARE @batchId VARCHAR(20) = FORMAT(SYSDATETIME(), 'yyyyMMddHHmmssffffff')

	
	BEGIN TRY

		--取出待處理項目
		SET @success = @success + 1
		
		INSERT INTO dbo.XXIF_CHP_P213_IN_BATCH_COM_ST ( 
			  PROCESS_CODE, SERVER_CODE, BATCH_ID, BATCH_LINE_ID, STATUS_CODE
			, ERROR_MSG, ORGCODE, BATCH_NO, ACTUAL_CMPLT_DATE
			, ATTRIBUTE1, ATTRIBUTE2, ATTRIBUTE3, ATTRIBUTE4, ATTRIBUTE5
			, ATTRIBUTE6, ATTRIBUTE7, ATTRIBUTE8, ATTRIBUTE9, ATTRIBUTE10
			, ATTRIBUTE11, ATTRIBUTE12, ATTRIBUTE13, ATTRIBUTE14, ATTRIBUTE15
			, REQUEST_ID, CREATED_BY, CREATION_DATE, LAST_UPDATED_BY, LAST_UPDATE_DATE
			, LAST_UPDATE_LOGIN
		)
		OUTPUT inserted.PROCESS_CODE, inserted.SERVER_CODE, inserted.BATCH_ID, inserted.BATCH_LINE_ID, inserted.BATCH_NO
			INTO @table (PROCESS_CODE, SERVER_CODE, BATCH_ID, BATCH_LINE_ID, BATCH_NO)
		SELECT
			@processCode, @serverCode, @batchId, ROW_NUMBER() OVER (ORDER BY (SELECT 1)), NULL
			, NULL, MIN(H.ORGANIZATION_CODE), MIN(BATCH_NO), ISNULL(MIN(H.LAST_UPDATE_DATE), MIN(H.CREATION_DATE))
			, NULL, NULL, NULL, NULL, NULL
			, NULL, NULL, NULL, NULL, NULL
			, NULL, NULL, NULL, NULL, NULL
			, NULL, -1, MIN(H.CREATION_DATE), -1, ISNULL(MIN(H.LAST_UPDATE_DATE), MIN(H.CREATION_DATE) )
			, -1
		FROM OSP_HEADER_T H 
		WHERE H.OSP_HEADER_ID = @ospHeaderId
		
		IF (@@ROWCOUNT <= 0 OR @@ERROR <> 0)
		BEGIN
			SET @message = 'PROCESS_CODE:' + @processCode + ' SERVER_CODE:' + @serverCode + ' BATCH_ID:' 
			+ @batchId + ' OSP_HEADER_ID:' + CONVERT(varchar, @ospHeaderId) + ' 上傳 生產批完工狀態變更 失敗'
			RAISERROR(@message, 16, @success)
		END

		SET @success = @success + 1
		INSERT INTO XXIF_CHP_CONTROL_ST (
			PROCESS_CODE, SERVER_CODE, BATCH_ID, ROW_NUM, PROCESS_DATE
			, REQUEST_ID, STATUS_CODE, ERROR_MSG, SOA_PULLING_FLAG, SOA_ERROR_MSG
			, SOA_PROCESS_CODE, CREATED_BY, CREATION_DATE, LAST_UPDATED_BY, LAST_UPDATE_DATE
			, LAST_UPDATE_LOGIN
		)
		SELECT 
			C.PROCESS_CODE, C.SERVER_CODE, C.BATCH_ID, COUNT(*) AS ROW_NUM, @txnDate
			, NULL, NULL, NULL, 'In-W', NULL
			, NULL, -1, MAX(C.CREATION_DATE), -1, MAX(C.LAST_UPDATE_DATE)
			, -1
		FROM XXIF_CHP_P213_IN_BATCH_COM_ST C
		JOIN @table T ON T.PROCESS_CODE = C.PROCESS_CODE AND T.SERVER_CODE = C.SERVER_CODE AND T.BATCH_ID = C.BATCH_ID AND T.BATCH_LINE_ID = C.BATCH_LINE_ID
		GROUP BY C.PROCESS_CODE, C.SERVER_CODE, C.BATCH_ID

		IF (@@ROWCOUNT <= 0 OR @@ERROR <> 0)
		BEGIN
			SET @message = 'PROCESS_CODE:' + @processCode + ' SERVER_CODE:' + @serverCode + ' BATCH_ID:' 
			+ @batchId + ' OSP_HEADER_ID:' + CONVERT(varchar, @ospHeaderId) + ' 寫入 XXIF_CHP_CONTROL_ST 失敗'
			RAISERROR(@message, 16, @success)
		END

		SET @success = @success + 1
		INSERT INTO OSP_SOA_S3_T (
			OSP_HEADER_ID, PROCESS_CODE, SERVER_CODE, BATCH_ID, STATUS_CODE
			, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE
		) 
		SELECT 
			@ospHeaderId, PROCESS_CODE, SERVER_CODE, BATCH_ID, NULL
			, @user, @txnDate, NULL, NULL
		FROM @table 
		GROUP BY PROCESS_CODE, SERVER_CODE, BATCH_ID

		IF(@@ROWCOUNT <= 0 OR @@ERROR <> 0)
		BEGIN
			SET @message = @message + ' 更新 OSP_SOA_S3_T 錯誤'
			RAISERROR(@message, 16, @success)
		END
		
		SET @code = 0
		SET @message = ''
		
	END TRY
	BEGIN CATCH
		SET @code = -1 * @success
		SET @message = CAST(@success AS VARCHAR(2)) + ':' + ERROR_MESSAGE()
	END CATCH

END

