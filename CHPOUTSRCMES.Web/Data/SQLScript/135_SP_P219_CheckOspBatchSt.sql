-- =============================================
-- Author:		Polo Lin
-- Create date: 2020/09/1
-- Description:	SOA OSP_BATCH_ST 資料接收
-- =============================================
CREATE PROCEDURE [dbo].[SP_P219_CheckOspBatchSt]
	@processCode NVARCHAR(20),
	@serverCode NVARCHAR(20),
	@batchId NVARCHAR(20),
	@code INT OUTPUT,
	@message VARCHAR(500) OUTPUT,
	@user VARCHAR(128)
AS
BEGIN

	DECLARE @controlStage TABLE (
		PROCESS_CODE NVARCHAR(20),
		SERVER_CODE NVARCHAR(20),
		BATCH_ID NVARCHAR(20),
		STATUS_CODE NVARCHAR(1),
		ERROR_MSG NVARCHAR(2000),
		SOA_PULLING_FLAG NVARCHAR(30),
		SOA_PROCESS_CODE NVARCHAR(30)
	)

	DECLARE @orgTable TABLE (
		PROCESS_CODE NVARCHAR(20),
		SERVER_CODE NVARCHAR(20),
		BATCH_ID NVARCHAR(20),
		BATCH_LINE_ID BIGINT,
		PE_BATCH_ID BIGINT,
		OSP_REMARK NVARCHAR(240),
		STATUS_CODE NVARCHAR(1),
		ERROR_MSG NVARCHAR(2000)
	)

	DECLARE @success INT = 0
	
	SET @message = 'PROCESS_CODE:' + @processCode + ' SERVER_CODE:' + @serverCode + ' BATCH_ID:' + @batchId

	BEGIN TRY
		
		--取出待處理項目
		SET @success = @success + 1
		INSERT INTO @controlStage (PROCESS_CODE, SERVER_CODE, BATCH_ID, STATUS_CODE, ERROR_MSG, SOA_PULLING_FLAG, SOA_PROCESS_CODE)
			SELECT PROCESS_CODE, SERVER_CODE, BATCH_ID, 'S' AS STATUS_CODE, '' AS ERROR_MSG, SOA_PULLING_FLAG, SOA_PROCESS_CODE FROM XXIF_CHP_CONTROL_ST 
			WHERE PROCESS_CODE = @processCode AND SERVER_CODE = @serverCode AND BATCH_ID = @batchId
		
		IF NOT EXISTS(SELECT TOP 1 * FROM @controlStage)
		BEGIN
			SET @message = @message + ' 無資料'
			RAISERROR(@message, 16, @success)
		END

		--取出加工明細，檢查資料是否正確?
		SET @success = @success + 1
		INSERT INTO @orgTable (
			PROCESS_CODE, SERVER_CODE, BATCH_ID, BATCH_LINE_ID, PE_BATCH_ID, OSP_REMARK, 
			STATUS_CODE, ERROR_MSG)
		SELECT 
			c.PROCESS_CODE, c.SERVER_CODE, c.BATCH_ID, c.BATCH_LINE_ID, c.PE_BATCH_ID, ISNULL(c.OSP_REMARK, ''),
			'S' AS STATUS_CODE,
			CASE WHEN r.ORGANIZATION_ID IS NULL THEN N'組織不存在!! ' WHEN r.CONTROL_FLAG = 'D' THEN N'組織已刪除' ELSE '' END +
			CASE WHEN c.LINE_TYPE = 'P' THEN '' WHEN c.LINE_TYPE = 'I' AND s.SUBINVENTORY_CODE IS NULL THEN N'倉庫不存在!! ' WHEN s.CONTROL_FLAG = 'D' THEN N'倉庫已刪除' ELSE '' END +
			CASE WHEN t.INVENTORY_ITEM_ID IS NULL THEN N'料號資料不存在!! ' WHEN t.CONTROL_FLAG = 'D' THEN N'料號已刪除' ELSE '' END +
			CASE WHEN c.TRANSACTION_QUANTITY IS NULL OR c.TRANSACTION_UOM IS NULL THEN N'交易單位不正確!! ' ELSE '' END +
			CASE WHEN c.PRIMARY_QUANTITY IS NULL OR c.PRIMARY_UOM IS NULL THEN N'主單位不正確!! ' ELSE '' END +
			CASE WHEN t.CATALOG_ELEM_VAL_070 = '平版' AND (c.SECONDARY_QUANTITY IS NULL OR c.SECONDARY_UOM IS NULL) THEN N'次單位不正確!! ' ELSE '' END +
			--CASE WHEN p.PE_BATCH_ID IS NOT NULL AND c.BATCH_STATUS = 2 THEN '加工單號:' + p.BATCH_NO + '已存在!!' ELSE '' END + 
			CASE WHEN c.BATCH_TYPE= 'BTH' AND c.PO_NUMBER IS NOT NULL AND c.PO_STATUS <> 'APPROVED' THEN '又裁又代 訂單狀態必須為APPROVED'
				WHEN c.BATCH_TYPE = 'BTH' AND c.PO_STATUS = 'APPROVED' AND M.BATCH_ID IS NULL THEN '又裁又代 未存在代紙單資料' 
				WHEN  c.BATCH_TYPE = 'BTH' AND c.PO_STATUS = 'APPROVED' AND M.BATCH_LINE_ID > O.BATCH_LINE_ID THEN '又裁又代 代紙單BATCH_LINE_ID 小於工單'
				ELSE '' END
			AS ERROR_MSG
		  FROM [XXIF_CHP_CONTROL_ST] ctl
		  JOIN @controlStage cs ON cs.PROCESS_CODE = ctl.PROCESS_CODE AND cs.SERVER_CODE = ctl.SERVER_CODE AND cs.BATCH_ID = ctl.BATCH_ID
		  JOIN XXIF_CHP_P219_OSP_BATCH_ST c ON c.PROCESS_CODE = ctl.PROCESS_CODE AND c.SERVER_CODE = ctl.SERVER_CODE AND c.BATCH_ID = ctl.BATCH_ID
		  LEFT JOIN OSP_ORG_T o ON o.PROCESS_CODE = c.PROCESS_CODE AND o.SERVER_CODE = c.SERVER_CODE AND o.BATCH_ID = c.BATCH_ID AND o.BATCH_LINE_ID = c.BATCH_LINE_ID
		  LEFT JOIN ITEMS_T t ON t.INVENTORY_ITEM_ID = c.INVENTORY_ITEM_ID 
		  LEFT JOIN ORGANIZATION_T r ON r.ORGANIZATION_ID = c.ORGANIZATION_ID
		  LEFT JOIN SUBINVENTORY_T s ON s.SUBINVENTORY_CODE = c.SUBINVENTORY
		  LEFT JOIN LOCATOR_T l ON l.LOCATOR_ID = c.LOCATOR_ID AND l.SUBINVENTORY_CODE = c.SUBINVENTORY 
		  LEFT JOIN OSP_HEADER_T p ON p.PE_BATCH_ID = c.PE_BATCH_ID
		  LEFT JOIN XXIF_CHP_P219_OSP_BATCH_ST M ON c.PROCESS_CODE = M.PROCESS_CODE AND c.SERVER_CODE = M.SERVER_CODE AND c.BATCH_ID = M.BATCH_ID 
					AND M.ATTRIBUTE7 = c.BATCH_NO AND M.PO_NUMBER IS NULL AND M.LINE_TYPE = c.LINE_TYPE AND M.BATCH_STATUS = c.BATCH_STATUS
		  WHERE ctl.PROCESS_CODE = @processCode 
		  AND ctl.SERVER_CODE = @serverCode
		  AND ctl.BATCH_ID = @batchId 

		IF NOT EXISTS(SELECT TOP 1 * FROM @orgTable)
		BEGIN
			SET @message = @message + ' 無資料'
			RAISERROR(@message, 16, @success)
		END

		--檢查 產品或組成成份 資料是否都存在
		UPDATE O SET ERROR_MSG = O.ERROR_MSG + ' 資料缺少產品或組成成份'
		FROM @orgTable O
		JOIN (
			SELECT 		
				O.PROCESS_CODE, O.SERVER_CODE, O.BATCH_ID, O.PE_BATCH_ID
				, (SELECT COUNT(*) FROM XXIF_CHP_P219_OSP_BATCH_ST WHERE PROCESS_CODE = O.PROCESS_CODE AND SERVER_CODE = O.SERVER_CODE AND BATCH_ID = O.BATCH_ID AND PE_BATCH_ID = O.PE_BATCH_ID AND LINE_TYPE = 'I') AS INVEST
				, (SELECT COUNT(*) FROM XXIF_CHP_P219_OSP_BATCH_ST WHERE PROCESS_CODE = O.PROCESS_CODE AND SERVER_CODE = O.SERVER_CODE AND BATCH_ID = O.BATCH_ID AND PE_BATCH_ID = O.PE_BATCH_ID AND LINE_TYPE = 'P') AS PRODUCT
			FROM XXIF_CHP_P219_OSP_BATCH_ST O
			WHERE O.PROCESS_CODE = @processCode AND O.SERVER_CODE = @serverCode AND O.BATCH_ID = @batchId
			GROUP BY O.PROCESS_CODE, O.SERVER_CODE, O.BATCH_ID, O.PE_BATCH_ID
		) A ON A.PROCESS_CODE = O.PROCESS_CODE AND A.SERVER_CODE = O.SERVER_CODE AND A.BATCH_ID = O.BATCH_ID AND A.PE_BATCH_ID = O.PE_BATCH_ID
		WHERE A.INVEST <=0 OR A.PRODUCT <= 0

		
		--處理完成回寫 XXIF_CHP_P219_OSP_BATCH_ST 
		SET @success = @success + 1
		UPDATE A SET STATUS_CODE = CASE WHEN LEN(B.ERROR_MSG) > 0 THEN 'E' ELSE 'S' END, ERROR_MSG = CASE WHEN LEN(B.ERROR_MSG) > 0 THEN B.ERROR_MSG ELSE NULL END
		FROM XXIF_CHP_P219_OSP_BATCH_ST A 
		JOIN @orgTable B ON B.PROCESS_CODE = A.PROCESS_CODE AND B.SERVER_CODE = A.SERVER_CODE AND B.BATCH_ID = A.BATCH_ID AND B.BATCH_LINE_ID = A.BATCH_LINE_ID

		IF(@@ROWCOUNT <= 0 OR @@ERROR <> 0)
		BEGIN
			SET @message = @message + ' 更新 XXIF_CHP_P219_OSP_BATCH_ST 錯誤'
			RAISERROR(@message, 16, @success)
		END

		IF EXISTS (SELECT * FROM XXIF_CHP_P219_OSP_BATCH_ST A 
					JOIN @orgTable B ON B.PROCESS_CODE = A.PROCESS_CODE AND B.SERVER_CODE = A.SERVER_CODE AND B.BATCH_ID = A.BATCH_ID AND B.BATCH_LINE_ID = A.BATCH_LINE_ID
					WHERE A.STATUS_CODE <> 'S')
		BEGIN
			SET @code = 1
			SET @message = '資料驗證失敗! 詳見明細'
		END
		ELSE
		BEGIN
			SET @code = 0
			SET @message = ''
		END
	END TRY
	BEGIN CATCH
		SET @code = -1 * @success
		SET @message = CAST(@success AS VARCHAR(2)) + ':' + ERROR_MESSAGE()

		UPDATE XXIF_CHP_P219_OSP_BATCH_ST SET STATUS_CODE = 'E', ERROR_MSG = @message
			WHERE PROCESS_CODE =@processCode AND SERVER_CODE = @serverCode AND BATCH_ID = @batchId

	END CATCH

END

