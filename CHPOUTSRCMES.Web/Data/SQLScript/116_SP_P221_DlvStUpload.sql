-- =============================================
-- Author:		Polo Lin
-- Create date: 2020/09/4
-- Description:	SOA SHIP_CONFIRM_ST 資料上傳
-- =============================================
CREATE PROCEDURE [dbo].[SP_P221_DlvStUpload]
	@tripId BIGINT,
	@processCode VARCHAR(20) OUTPUT,
	@serverCode VARCHAR(20) OUTPUT,
	@batchId VARCHAR(20) OUTPUT,
	@code INT OUTPUT,
	@message VARCHAR(500) OUTPUT,
	@user VARCHAR(128)
AS
BEGIN

	DECLARE @table TABLE (
		PROCESS_CODE VARCHAR(20)
		, SERVER_CODE VARCHAR(20)
		, BATCH_ID VARCHAR(20)
		, BATCH_LINE_ID BIGINT
		, TRIP_ID BIGINT 
	) 

	DECLARE @success INT = 0
	DECLARE @count INT = 0
	SELECT @message = 'TRIP_ID:' + CONVERT(varchar, @tripId)

	DECLARE @txnDate DATETIME = GETDATE()
	SET @processCode = 'XXIFP221'
	SET @serverCode = 'FTY'
	SET @batchId = FORMAT(@txnDate, 'yyyyMMddHHmmssffffff')

	BEGIN TRY
		
		--取出待處理項目
		SET @success = @success + 1

		INSERT INTO XXIF_CHP_P221_SHIP_CONFIRM_ST (
		PROCESS_CODE, SERVER_CODE, BATCH_ID, BATCH_LINE_ID, STATUS_CODE
			, ERROR_MSG, ORG_ID, ORGANIZATION_ID, TRIP_ID, DELIVERY_ID
			, TRANSACTION_DATE, DELIVERY_DETAIL_ID, INVENTORY_ITEM_ID
			, TRANSACTION_QUANTITY
			, TRANSACTION_UOM
			, PRIMARY_QUANTITY
			, PRIMARY_UOM
			, SECONDARY_QUANTITY
			, SECONDARY_UOM
			, ROLL_NUMBER
			, ROLL_QUANTITY
			, REMARK, ATTRIBUTE1, ATTRIBUTE2, ATTRIBUTE3
			, ATTRIBUTE4, ATTRIBUTE5, ATTRIBUTE6, ATTRIBUTE7, ATTRIBUTE8
			, ATTRIBUTE9, ATTRIBUTE10, ATTRIBUTE11, ATTRIBUTE12, ATTRIBUTE13
			, ATTRIBUTE14, ATTRIBUTE15, REQUEST_ID, CREATED_BY, CREATION_DATE
			, LAST_UPDATED_BY, LAST_UPDATE_DATE
		)
		OUTPUT inserted.PROCESS_CODE, inserted.SERVER_CODE, inserted.BATCH_ID, inserted.BATCH_LINE_ID, inserted.TRIP_ID 
			INTO @table (PROCESS_CODE, SERVER_CODE, BATCH_ID, BATCH_LINE_ID, TRIP_ID)
		SELECT 
			@processCode, @serverCode, @batchId, ROW_NUMBER() OVER (ORDER BY (SELECT 1)), NULL
			, NULL,MIN(H.ORG_ID), MIN(H.ORGANIZATION_ID), H.TRIP_ID, H.DELIVERY_ID
			, @txnDate, D.DELIVERY_DETAIL_ID, MIN(D.INVENTORY_ITEM_ID)
			, MIN(D.REQUESTED_TRANSACTION_QUANTITY)
			, MIN(D.REQUESTED_TRANSACTION_UOM)
			, MIN(D.REQUESTED_PRIMARY_QUANTITY)
			, MIN(D.REQUESTED_PRIMARY_UOM)
			, MIN(D.REQUESTED_SECONDARY_QUANTITY)
			, MIN(D.REQUESTED_SECONDARY_UOM)
			, dbo.ROLL_NUMBER(MIN(S.ITEM_CATEGORY), P.LOT_NUMBER) AS ROLL_NUMBER
			, dbo.ROLL_QUANTITY(MIN(S.ITEM_CATEGORY), MIN(P.PRIMARY_QUANTITY)) AS ROLL_QUANTITY
			, MIN(H.NOTE), NULL, NULL, NULL
			, NULL, NULL, NULL, NULL, NULL
			, NULL, NULL, NULL, NULL, NULL
			, NULL, NULL, NULL, -1, MIN(P.CREATION_DATE)
			, -1, ISNULL(MIN(P.LAST_UPDATE_DATE), MIN(P.CREATION_DATE))
		FROM DLV_HEADER_T H
		JOIN DLV_DETAIL_HT D ON D.DLV_HEADER_ID = H.DLV_HEADER_ID
		JOIN DLV_PICKED_HT P ON P.DLV_DETAIL_ID = D.DLV_DETAIL_ID
		JOIN STOCK_T S ON S.STOCK_ID = P.STOCK_ID
		WHERE H.TRIP_ID = @tripId
		GROUP BY H.TRIP_ID, H.DELIVERY_ID, D.DELIVERY_DETAIL_ID, P.LOT_NUMBER
		ORDER BY H.TRIP_ID, H.DELIVERY_ID, D.DELIVERY_DETAIL_ID, P.LOT_NUMBER
		
		IF (@@ROWCOUNT <= 0 AND @@ERROR <> 0)
		BEGIN
			SET @message = 'PROCESS_CODE:' + @processCode + ' SERVER_CODE:' + @serverCode + ' BATCH_ID:' 
			+ @batchId + ' TRIP_ID:' + CONVERT(varchar, @tripId) + ' 上傳出貨失敗'
			RAISERROR(@message, 16, @success)
		END
		
		SET @code = 0
		SET @message = ''
		
	END TRY
	BEGIN CATCH
		SET @code = -1 * @success
		SET @message = CAST(@success AS VARCHAR(2)) + ':' + ERROR_MESSAGE()
	END CATCH

END

