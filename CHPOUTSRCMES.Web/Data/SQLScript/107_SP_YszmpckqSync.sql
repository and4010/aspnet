-- =============================================
-- Author:		Polo Lin
-- Create date: 2020/08/14
-- Description:	令重包數資料同步(YSZMPCKQ_T 及 YSZMPCKQ_TMP_T)
-- =============================================
CREATE PROCEDURE [dbo].[SP_YszmpckqSync]
	-- Add the parameters for the stored procedure here
AS
BEGIN
	
	DECLARE @deletelist TABLE (
		ORGANIZATION_ID BIGINT,
		ORGANIZATION_CODE NVARCHAR(3),
		OSP_SUBINVENTORY NVARCHAR(10),
		PSTYP NVARCHAR(4),
		BWETUP DECIMAL(30, 10),
		BWETDN DECIMAL(30, 10),
		RWTUP DECIMAL(30, 10),
		RWTDN DECIMAL(30, 10),
		PCKQ BIGINT,
		PAPER_QTY BIGINT,
		PIECES_QTY BIGINT,
		CREATED_BY BIGINT,
		CREATION_DATE DATETIME,
		LAST_UPDATE_BY BIGINT,
		LAST_UPDATE_DATE DATETIME
	);
	
	DECLARE @mergelist TABLE (
		ORGANIZATION_ID BIGINT,
		ORGANIZATION_CODE NVARCHAR(3),
		OSP_SUBINVENTORY NVARCHAR(10),
		PSTYP NVARCHAR(4),
		BWETUP DECIMAL(30, 10),
		BWETDN DECIMAL(30, 10),
		RWTUP DECIMAL(30, 10),
		RWTDN DECIMAL(30, 10),
		PCKQ BIGINT,
		PAPER_QTY BIGINT,
		PIECES_QTY BIGINT,
		CREATED_BY BIGINT,
		CREATION_DATE DATETIME,
		LAST_UPDATE_BY BIGINT,
		LAST_UPDATE_DATE DATETIME,
		CONTROL_FLAG CHAR(1)
	);

	DECLARE @tableVar TABLE (
		MergeAction VARCHAR(20), 
		InsertedID  NVARCHAR(30), 
		DeletedID  NVARCHAR(30)
	);
	DECLARE @success INT = 0, @code INT = 0, @message NVARCHAR(500) = '';
	DECLARE @deleteCount INT = 0, @updateCount INT = 0, @insertCount INT = 0;
	
	
	BEGIN TRY
		SET @success = @success + 1
		--收集需要刪除的項目
		INSERT INTO @deletelist (
			ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
			BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
			PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE
		) 
		SELECT 
			ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
			BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
			PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE
		FROM (
			SELECT 
				ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
				BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
				PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE
			FROM YSZMPCKQ_T O
			WHERE CONTROL_FLAG <> 'D'
			EXCEPT
			SELECT 
				ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
				BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
				PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE
			FROM YSZMPCKQ_TMP_T T
		) A

		--如有須刪除的 更新CONTROL_FLAG = D
		IF EXISTS (SELECT TOP 1 * FROM @deletelist)
		BEGIN
			--更新CONTROL_FLAG = D
			DELETE FROM YSZMPCKQ_T WHERE YSZMPCKQ_ID IN  (
				SELECT Y.YSZMPCKQ_ID FROM @deletelist D 
				JOIN YSZMPCKQ_T Y 
				ON  Y.ORGANIZATION_ID = D.ORGANIZATION_ID
				AND Y.ORGANIZATION_CODE = D.ORGANIZATION_CODE
				AND Y.OSP_SUBINVENTORY = D.OSP_SUBINVENTORY
				AND Y.PSTYP = D.PSTYP
				AND Y.BWETUP = D.BWETUP
				AND Y.BWETDN = D.BWETDN
				AND Y.RWTUP = D.RWTUP
				AND Y.RWTDN = D.RWTDN
				AND Y.PCKQ = D.PCKQ
				AND Y.PAPER_QTY = D.PAPER_QTY
				AND Y.PIECES_QTY = D.PIECES_QTY
			)

			SELECT @deleteCount = @@ROWCOUNT

			--失敗，引發EXCEPTION 記錄問題
			IF(@deleteCount <= 0 OR @@ERROR <> 0)
			BEGIN
				SELECT @message = '刪除，更新CONTROL_FLAG失敗 ERROR:' + CAST(@@ERROR AS VARCHAR(100))
				RAISERROR(@message, 16, @success)
			END
		END


		SET @success = @success + 1
		--收集需要更新或新增的項目
		INSERT INTO @mergelist (
				ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
				BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
				PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE, 
				CONTROL_FLAG) 
		SELECT	ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
				BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
				PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE, 
				CONTROL_FLAG 
		FROM (
			SELECT ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
				BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
				PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE, 
				CONTROL_FLAG 
			FROM YSZMPCKQ_TMP_T T
			EXCEPT
			SELECT ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
				BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
				PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE, 
				CONTROL_FLAG 
			FROM YSZMPCKQ_T O
		) A

		IF EXISTS (SELECT TOP 1 * FROM @mergelist)
		BEGIN
			MERGE INTO YSZMPCKQ_T WITH (HOLDLOCK) AS A 
				USING @mergelist AS B
				ON (A.ORGANIZATION_ID = B.ORGANIZATION_ID
				AND A.ORGANIZATION_CODE = B.ORGANIZATION_CODE
				AND A.OSP_SUBINVENTORY = B.OSP_SUBINVENTORY
				AND A.PSTYP = B.PSTYP
				AND A.BWETUP = B.BWETUP
				AND A.BWETDN = B.BWETDN
				AND A.RWTUP = B.RWTUP
				AND A.RWTDN = B.RWTDN
				AND A.PCKQ = B.PCKQ
				AND A.PAPER_QTY = B.PAPER_QTY
				AND A.PIECES_QTY = B.PIECES_QTY)
				--更新CONTROL_FLAG = '' 及其它欄位值
				WHEN MATCHED THEN 
					UPDATE SET 
						A.CONTROL_FLAG = '', A.CREATED_BY = B.CREATED_BY, A.CREATION_DATE = B.CREATION_DATE, 
						A.LAST_UPDATE_BY = B.LAST_UPDATE_BY, A.LAST_UPDATE_DATE = B.LAST_UPDATE_DATE
				--新增資料
				WHEN NOT MATCHED THEN 
					INSERT (
						ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
						BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
						PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE, 
						CONTROL_FLAG 
					) VALUES (
						ORGANIZATION_ID, ORGANIZATION_CODE, OSP_SUBINVENTORY, PSTYP, BWETUP, 
						BWETDN, RWTUP, RWTDN, PCKQ, PAPER_QTY, 
						PIECES_QTY, CREATED_BY, CREATION_DATE, LAST_UPDATE_BY, LAST_UPDATE_DATE, 
						'' 
					)
				OUTPUT $action, inserted.YSZMPCKQ_ID 'inserted', deleted.YSZMPCKQ_ID 'deleted' INTO @tableVar;

			SELECT @insertCount = COUNT(*) FROM @tableVar WHERE MergeAction ='INSERT'
			SELECT @updateCount = COUNT(*) FROM @tableVar WHERE MergeAction ='UPDATE'

			--失敗，引發EXCEPTION 記錄問題
			IF ((@insertCount + @updateCount) <= 0 OR @@ERROR <> 0)
			BEGIN
				SELECT @message = '更新，更新或新增資料失敗 ERROR:' + CAST(@@ERROR AS VARCHAR(100)) + ' COUNT:' + CAST(@@ROWCOUNT AS VARCHAR(100))
				RAISERROR(@message, 16, @success)
			END 

			
		END

		SET @code = 0
		SET @message = ''
		
	END TRY
	BEGIN CATCH
		SET @code = -1 * @success
		SET @message = CAST(@success AS VARCHAR(2)) + ':' + ERROR_MESSAGE()
	END CATCH

	SELECT @code 'CODE', @message 'MESSAGE', @insertCount 'INSERT_COUNT', @updateCount 'UPDATE_COUNT', @deleteCount 'DELETE_COUNT'

END

